# prints a marble as three decimal digits
}0
Dp

# prints one marble as three decimal digits
:Dp
}0 .. ..
De co ut
+O +O +O
+O +O +O

# returns three decimal digits representing one input marble
:Decout
}0 }0 }0
/A /A %A
/A %A ..
{0 {1 {2

# divide by ten
# http://stackoverflow.com/a/19076173/13675
:/A
.. }0 .. }0 .. # right }0 is used for calculating r at the bottom
.. >> }0 .. .. 
.. >> >> \\ .. # q = (}0 >> 1) + (}0 >> 2)
.. .. // .. //
.. /\ >> \\ ..
.. .. >> .. //
.. .. >> \\ ..
.. .. >> .. ..
\\ >> // .. .. # q = q + (q >> 4)
.. >> .. .. ..
.. >> .. .. ..
.. /\ .. .. .. # q = q >> 3
.. .. /\ << ..
.. .. .. << ..
.. \\ << // .. # q = (q + (q << 2)) << 1
.. .. ~~ .. .. 
.. .. ++ .. .. # ~~ and ++ make unary negation
.. .. \\ .. // # r = -q + }0
\\ .. .. >9 \/
.. \\ .. ?0 ..
.. .. .. ++ ..
.. .. \\ .. .. # q = q + (r>9)
.. .. .. {0 .. # {0 = q

# modulo ten
# while(}0>10){ }0-=10 } {0=}0
:%A
@0 ..
}0 ..
<A -A
{0 @0
